grammar trichterwolke.init.Init with org.eclipse.xtext.common.Terminals
generate init "http://www.init.trichterwolke/Init"

DomainModel:
	('namespace' namespace=QualifiedName)?
	('defaultkeytype' defaultKeyType=IntegerType)?
	declarations+=Declaration*;

QualifiedName:
	ID ('.' ID)*;

Declaration:
	Enumeration | Entity;
	 	 
Enumeration:
	'enum' name=ID (':' superType=IntegerType)? '{'
		enumerals+=Enumeral*
	'}';

Enumeral:
	name=ID '=' value=INT;
	
Entity:
	'entity' name=ID (':' overrideKeyType=IntegerType)? '{'
	attributes+=Attribute*
	
	manies+=Many*
	'}';	
		
Attribute:
	name=ID ':' type=Type (key?='key' | unique?='unique')?; 
	
Many:
	'many' name=ID ':' type=DefinedType '{'
	attributes+=Attribute*	
	'}';	

Key:
	type=DefinedType;	
	 
	 
Type:
  (DefinedType | StandardType) (nullable?='?')?;
	
DefinedType:
	type=[Declaration];
		
StandardType:
	SimpleType | CharacterType;

SimpleType:
	FloatingpointType | IntegerType | OtherType;

CharacterType:
	keyword=CharacterKeyword ('(' size=INT ')')?;

enum CharacterKeyword:
	char;

FloatingpointType:
	keyword=FloatingPointKeyword;
 
enum FloatingPointKeyword:
	single | double | decimal;

IntegerType:
	keyword=IntegerKeyword;
	
enum IntegerKeyword:
	byte | short | int | long;	
  
OtherType:
	keyword=OtherKeyword;

enum OtherKeyword:
	datetime | timestamp | bool | guid;